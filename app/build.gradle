apply plugin: 'com.android.application'
if (getGradle().getStartParameter().getTaskRequests().toString().contains("standard")) {
    apply plugin: 'io.fabric'
}

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.gianlu.dnshero"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 13
        versionName "1.2.3"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'main'
    productFlavors {
        foss {
            dimension 'main'
        }

        standard {
            dimension 'main'
        }
    }
}

dependencies {
    standardApi(project(':CommonUtils')) {
        exclude group: 'com.getkeepsafe.taptargetview'
    }

    fossApi(project(':CommonUtils')) {
        exclude group: 'com.android.billingclient'
        exclude group: 'com.google.firebase'
        exclude group: 'com.crashlytics.sdk.android'
        exclude group: 'com.getkeepsafe.taptargetview'
    }

    api 'com.squareup.okhttp3:okhttp:3.11.0'
    api 'com.android.support:cardview-v7:28.0.0'
}

if (getGradle().getStartParameter().getTaskRequests().toString().contains("standard")) {
    apply plugin: 'com.google.gms.google-services'
}
